/*
 * Copyright 2015-2018 The twitlatte authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.2"

    defaultConfig {
        applicationId 'com.github.moko256.twitlatte'
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 13
        versionName '1.4.1'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        Base64.Decoder e = Base64.decoder

        buildConfigField 'String', 'CONSUMER_KEY', "\"${new String(e.decode(e.decode(cs)))}\""
        buildConfigField 'String', 'CONSUMER_SECRET', "\"${new String(e.decode(e.decode(ck + cc)))}\""

        buildConfigField 'int', 'CACHE_DATABASE_VERSION' , '2'

        vectorDrawables.useSupportLibrary = true
        resConfigs 'en', 'ja', 'ldltr'
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'shrinker-rules.pro'
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude '/lib/**'
        exclude '/javax/**'
        exclude '/okhttp3/**'
        exclude '/kotlin/**'
        exclude '/META-INF/**.kotlin_module'
        exclude '/META-INF/proguard/**'
        exclude '/autolink.yml'
        exclude '/extract.yml'
        exclude '/hit_highlighting.yml'
        exclude '/tld_lib.yml'
        exclude '/tlds.yml'
        exclude '/validate.yml'
    }
}

dependencies {

    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"
    implementation "com.android.support:gridlayout-v7:$support_library_version"
    implementation "com.android.support:preference-v7:$support_library_version"
    implementation "com.android.support:support-v13:$support_library_version"
    implementation "com.android.support:preference-v14:$support_library_version"
    implementation "com.android.support:customtabs:$support_library_version"

    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation "android.arch.lifecycle:viewmodel:$lifecycle_version"

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"
    implementation "com.google.android.exoplayer:extension-okhttp:$exoplayer_version"

    implementation "com.google.code.gson:gson:$gson_version"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"

    implementation 'jp.wasabeef:glide-transformations:3.3.0'

    implementation project(':mastodon4j_to_twitter4j')

    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    implementation 'com.github.chuross:flinglayout:0.5.0'

    implementation project(':japng-android:android_api')

    implementation 'com.twitter.twittertext:twitter-text:2.0.10'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.github.adelnizamutdinov:kotlin-either:2.0.1'

    // Android 4.4.2 uses 1.2. That's why
    //noinspection GradleDependency
    compileOnly 'org.ccil.cowan.tagsoup:tagsoup:1.2'

}

buildscript {
    ext.cc = "JhMlEyUkZoTWFHdERibkl4V0hCSmFYaEhNMXBUZFZVM2Jubz0="
    ext.cs = "YjNCSE5reGhVWFJQZW5KSk1XRTJjSGhJWVVOdlVqbEpSQT09"
    ext.ck = "U2xveVduVmlXWGhPZW5SaFduVjROM1pPYmpoQ1VsRn"
}